/*
 * Copyright 2016-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// tag::plugins[]
plugins {
    id 'java'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'com.github.hierynomus.license' version '0.14.0'
}

apply from: 'gradle/javafx-plugin.gradle'
apply from: 'gradle/integration-test.gradle'
apply from: 'gradle/functional-test.gradle'
// end::plugins[]

// tag::javafx[]
jfx {
    mainClass         = 'org.example.Launcher'
    allPermissions    = true
    keyStoreAlias     = 'example-user'
    keyStorePassword  = 'example-password'
    jfxMainAppJarName = jar.archiveName
    appName           = project.name
    vendor            = project.name
}
// end::javafx[]

// tag::dependencies[]
repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile "org.codehaus.griffon:griffon-javafx:${griffonVersion}"
    compile "org.codehaus.griffon:griffon-guice:${griffonVersion}"
    compile 'org.kordamp.ikonli:ikonli-javafx:2.1.1'
    compile 'org.kordamp.ikonli:ikonli-fontawesome-pack:2.1.1'
    compile 'org.kordamp.bootstrapfx:bootstrapfx-core:0.2.2'

    runtime 'org.slf4j:slf4j-simple:1.7.25'

    testCompile "org.codehaus.griffon:griffon-javafx-test:${griffonVersion}"
    testCompile 'pl.pragmatists:JUnitParams:1.1.1'
    testCompile 'org.mockito:mockito-core:2.15.1'
}
// end::dependencies[]

// tag::resources[]
processResources {
    filesMatching(['**/*.properties','**/*.xml']) {
        expand(
            'application_name'   : project.name,
            'application_version': project.version,
            'griffon_version'    : griffonVersion
        )
    }
}
// end::resources[]

license {
    header = rootProject.file('config/HEADER')
    strictCheck = true
    ignoreFailures = true
    mapping {
        java = 'SLASHSTAR_STYLE'
        fxml = 'XML_STYLE'
    }
    ext.year = '2016-2018'
    exclude '**/*.png'
}